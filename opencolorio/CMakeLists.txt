
bext_enable(OPENCOLORIO)

if (ENABLE_OPENCOLORIO)

  git_submodule_init(OpenColorIO CMakeLists.txt)

  RegisterDeps(OPENCOLORIO)
  if (TARGET ZLIB_BLD)
    set(Z_PREFIX_STR "brl_")
  endif (TARGET ZLIB_BLD)

  ExternalProject_Add(OPENCOLORIO_BLD
    URL "${CMAKE_CURRENT_SOURCE_DIR}/OpenColorIO"
    BUILD_ALWAYS ${EXT_BUILD_ALWAYS} ${LOG_OPTS}
    PATCH_COMMAND ${PATCH_EXECUTABLE};-E;-p1;${PATCH_OPTIONS};-i;${CMAKE_CURRENT_SOURCE_DIR}/opencolorio.patch
    COMMAND ${CMAKE_COMMAND};-E;copy;${PROJECT_SOURCE_DIR}/CMake/FindZLIB.cmake;<SOURCE_DIR>/share/cmake/modules/FindZLIB.cmake
    CMAKE_ARGS
    ${BUILD_TYPE_SPECIFIER}
    $<$<BOOL:${ZLIB_TARGET}>:-DZ_PREFIX=ON>
    $<$<BOOL:${ZLIB_TARGET}>:-DZ_PREFIX_STR=${Z_PREFIX_STR}>
    -DZLIB_ROOT=$<$<BOOL:${ZLIB_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -Dzstd_ROOT=$<$<BOOL:${ZLIB_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -Dpystring_ROOT=$<$<BOOL:${PYSTRING_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -Dminizip_ROOT=$<$<BOOL:${MINIZIP_NG_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -Dminizip_DIR=$<$<BOOL:${MINIZIP_NG_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}/lib/cmake/minizip>
    -Dexpat_ROOT=$<$<BOOL:${EXPAT_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -Dyaml-cpp_ROOT=$<$<BOOL:${YAML_CPP_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -DBUILD_TESTING=OFF
    -DOCIO_BUILD_APPS=OFF
    -DOCIO_BUILD_DOCS=OFF
    -DOCIO_BUILD_GPU_TESTS=OFF
    -DOCIO_BUILD_PYTHON=OFF
    -DOCIO_BUILD_TESTS=OFF
    -DOCIO_INSTALL_EXT_PACKAGES=NONE
    -DBIN_DIR=${BIN_DIR}
    -DLIB_DIR=${LIB_DIR}
    -DBUILD_STATIC_LIBS=${BUILD_STATIC_LIBS}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_BUNDLE_INSTALL_PREFIX}
    -DCMAKE_INSTALL_LIBDIR:PATH=${LIB_DIR}
    -DCMAKE_INSTALL_RPATH=${CMAKE_BUNDLE_INSTALL_PREFIX}/${LIB_DIR}
    LOG_CONFIGURE ${EXT_BUILD_QUIET}
    LOG_BUILD ${EXT_BUILD_QUIET}
    LOG_INSTALL ${EXT_BUILD_QUIET}
    LOG_OUTPUT_ON_FAILURE ${EXT_BUILD_QUIET}
    STEP_TARGETS install
    )

  TargetInstallDeps(OPENCOLORIO OPENCOLORIO_DEPENDS)

  # Copy the license into position in CMAKE_BUNDLE_INSTALL_PREFIX
  configure_file(
    ${CMAKE_CURRENT_SOURCE_DIR}/OpenColorIO/LICENSE
    ${DOC_LICENSE_DIR}/opencolorio.txt
    COPYONLY
    )

endif (ENABLE_OPENCOLORIO)

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

