
bext_enable(OPENCOLORIO)

if (ENABLE_OPENCOLORIO)

  git_submodule_init(OpenColorIO CMakeLists.txt)

  RegisterDeps(OPENCOLORIO)

  ExternalProject_Add(OPENCOLORIO_BLD
    URL "${CMAKE_CURRENT_SOURCE_DIR}/OpenColorIO"
    BUILD_ALWAYS ${EXT_BUILD_ALWAYS} ${LOG_OPTS}
    PATCH_COMMAND ${PATCH_EXECUTABLE};-E;-p1;${PATCH_OPTIONS};-i;${CMAKE_CURRENT_SOURCE_DIR}/opencolorio.patch
    CMAKE_ARGS
    -DCMAKE_FIND_PACKAGE_PREFER_CONFIG=ON
    ${BUILD_TYPE_SPECIFIER}
    $<$<BOOL:${EXPAT_TARGET}>:-Dexpat_ROOT=${CMAKE_BUNDLE_INSTALL_PREFIX}>
    $<$<BOOL:${MINIZIP_NG_TARGET}>:-Dminizip_DIR=${CMAKE_BUNDLE_INSTALL_PREFIX}/lib/cmake/minizip>
    $<$<BOOL:${MINIZIP_NG_TARGET}>:-Dminizip_ROOT=${CMAKE_BUNDLE_INSTALL_PREFIX}>
    $<$<BOOL:${PYSTRING_TARGET}>:-Dpystring_ROOT=${CMAKE_BUNDLE_INSTALL_PREFIX}>
    $<$<BOOL:${YAML_CPP_TARGET}>:-Dyaml-cpp_ROOT=${CMAKE_BUNDLE_INSTALL_PREFIX}>
    $<$<BOOL:${ZLIB_TARGET}>:-DZLIB_ROOT=${CMAKE_BUNDLE_INSTALL_PREFIX}>
    $<$<BOOL:${ZSTD_TARGET}>:-Dzstd_ROOT=${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -DBUILD_TESTING=OFF
    -DOCIO_BUILD_APPS=OFF
    -DOCIO_BUILD_DOCS=OFF
    -DOCIO_BUILD_GPU_TESTS=OFF
    -DOCIO_BUILD_PYTHON=OFF
    -DOCIO_BUILD_TESTS=OFF
    -DOCIO_INSTALL_EXT_PACKAGES=NONE
    -DBIN_DIR=${BIN_DIR}
    -DLIB_DIR=${LIB_DIR}
    -DBUILD_STATIC_LIBS=${BUILD_STATIC_LIBS}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_BUNDLE_INSTALL_PREFIX}
    -DCMAKE_INSTALL_LIBDIR:PATH=${LIB_DIR}
    -DCMAKE_INSTALL_RPATH=${CMAKE_BUNDLE_INSTALL_PREFIX}/${LIB_DIR}
    LOG_CONFIGURE ${EXT_BUILD_QUIET}
    LOG_BUILD ${EXT_BUILD_QUIET}
    LOG_INSTALL ${EXT_BUILD_QUIET}
    LOG_OUTPUT_ON_FAILURE ${EXT_BUILD_QUIET}
    STEP_TARGETS install
    )

  TargetInstallDeps(OPENCOLORIO OPENCOLORIO_DEPENDS)

  # Copy the license into position in CMAKE_BUNDLE_INSTALL_PREFIX
  configure_file(
    ${CMAKE_CURRENT_SOURCE_DIR}/OpenColorIO/LICENSE
    ${DOC_LICENSE_DIR}/opencolorio.txt
    COPYONLY
    )

endif (ENABLE_OPENCOLORIO)

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

