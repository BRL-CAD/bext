# Unless we have ENABLE_ALL set, base the building of perplex on
# the system detection results
if (ENABLE_ALL AND NOT DEFINED ENABLE_PERPLEX)
  set(ENABLE_PERPLEX ON)
endif (ENABLE_ALL AND NOT DEFINED ENABLE_PERPLEX)

if (NOT ENABLE_PERPLEX)

  find_package(PERPLEX)

  if (NOT PERPLEX_FOUND AND NOT DEFINED ENABLE_PERPLEX)
    set(ENABLE_PERPLEX "ON" CACHE BOOL "Enable perplex build")
  endif (NOT PERPLEX_FOUND AND NOT DEFINED ENABLE_PERPLEX)

endif (NOT ENABLE_PERPLEX)
set(ENABLE_PERPLEX "${ENABLE_PERPLEX}" CACHE BOOL "Enable perplex build")

if (ENABLE_PERPLEX)

  git_submodule_init(perplex CMakeLists.txt)

  set(PERPLEX_DEPENDS LEMON RE2C)
  TargetVars(PERPLEX_DEPENDS)

    ExternalProject_Add(PERPLEX_BLD
    URL "${CMAKE_CURRENT_SOURCE_DIR}/perplex"
    BUILD_ALWAYS ${EXT_BUILD_ALWAYS} ${LOG_OPTS}
    CMAKE_ARGS
    ${BUILD_TYPE_SPECIFIER}
    -DLEMON_ROOT=$<$<BOOL:${LEMON_TARGET}>:${CMAKE_NOBUNDLE_INSTALL_PREFIX}>
    -DRE2C_ROOT=$<$<BOOL:${RE2C_TARGET}>:${CMAKE_NOBUNDLE_INSTALL_PREFIX}>
    -DBIN_DIR=${BIN_DIR}
    -DLIB_DIR=${LIB_DIR}
    -DDATA_DIR=${DATA_DIR}
    -DBUILD_STATIC_LIBS=${BUILD_STATIC_LIBS}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_NOBUNDLE_INSTALL_PREFIX}
    -DCMAKE_INSTALL_LIBDIR=${LIB_DIR}
    -DCMAKE_INSTALL_RPATH=${CMAKE_BUNDLE_INSTALL_PREFIX}/${LIB_DIR}
    LOG_CONFIGURE ${EXT_BUILD_QUIET}
    LOG_BUILD ${EXT_BUILD_QUIET}
    LOG_INSTALL ${EXT_BUILD_QUIET}
    LOG_OUTPUT_ON_FAILURE ${EXT_BUILD_QUIET}
    STEP_TARGETS install
    )

  TargetInstallDeps(PERPLEX PERPLEX_DEPENDS)

  if (TARGET PATCH_BLD)
    ExternalProject_Add_StepDependencies(PERPLEX_BLD configure PATCH_BLD-install)
  endif (TARGET PATCH_BLD)

endif (ENABLE_PERPLEX)

# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

