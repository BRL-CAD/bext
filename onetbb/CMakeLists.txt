# Unless we have ENABLE_ALL set, based the building of oneTBB on
# the system detection results
if (ENABLE_ALL AND NOT DEFINED ENABLE_ONETBB)
  set(ENABLE_ONETBB ON)
endif (ENABLE_ALL AND NOT DEFINED ENABLE_ONETBB)

if (NOT ENABLE_ONETBB)

  find_package(TBB)

  if (NOT TBB_FOUND AND NOT DEFINED ENABLE_ONETBB)
    set(ENABLE_ONETBB "ON" CACHE BOOL "Enable oneTBB build")
  endif (NOT TBB_FOUND AND NOT DEFINED ENABLE_ONETBB)

endif (NOT ENABLE_ONETBB)
set(ENABLE_ONETBB "${ENABLE_ONETBB}" CACHE BOOL "Enable oneTBB build")

if (ENABLE_ONETBB)

  git_submodule_init(oneTBB CMakeLists.txt)

  set(ONETBB_DEPENDS PATCH)
  TargetVars(ONETBB_DEPENDS)

  ExternalProject_Add(ONETBB_BLD
    URL "${CMAKE_CURRENT_SOURCE_DIR}/oneTBB"
    BUILD_ALWAYS ${EXT_BUILD_ALWAYS} ${LOG_OPTS}
    CMAKE_ARGS
    ${BUILD_TYPE_SPECIFIER}
    -DTBB_TEST=OFF
    -DBIN_DIR=${BIN_DIR}
    -DLIB_DIR=${LIB_DIR}
    -DBUILD_STATIC_LIBS=${BUILD_STATIC_LIBS}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_BUNDLE_INSTALL_PREFIX}
    -DCMAKE_INSTALL_LIBDIR:PATH=${LIB_DIR}
    -DCMAKE_INSTALL_RPATH=${CMAKE_BUNDLE_INSTALL_PREFIX}/${LIB_DIR}
    LOG_CONFIGURE ${EXT_BUILD_QUIET}
    LOG_BUILD ${EXT_BUILD_QUIET}
    LOG_INSTALL ${EXT_BUILD_QUIET}
    LOG_OUTPUT_ON_FAILURE ${EXT_BUILD_QUIET}
    STEP_TARGETS install
    )

  TargetInstallDeps(ONETBB ONETBB_DEPENDS)

  # Copy the license into position in CMAKE_BUNDLE_INSTALL_PREFIX
  file(MAKE_DIRECTORY ${CMAKE_BUNDLE_INSTALL_PREFIX}/doc/legal/other)
  configure_file(
    ${CMAKE_CURRENT_SOURCE_DIR}/oneTBB/LICENSE.txt
    ${CMAKE_BUNDLE_INSTALL_PREFIX}/doc/legal/other/oneTBB.txt
    COPYONLY
    )

endif (ENABLE_ONETBB)

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

