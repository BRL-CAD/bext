# Unless we have ENABLE_ALL set, based the building of BOOST on
# the system detection results
if (ENABLE_ALL AND NOT DEFINED ENABLE_BOOST)
  set(ENABLE_BOOST ON)
endif (ENABLE_ALL AND NOT DEFINED ENABLE_BOOST)

if (NOT ENABLE_BOOST)

  find_package(Boost)

  if (NOT Boost_FOUND AND NOT DEFINED ENABLE_BOOST)
    set(ENABLE_BOOST "ON" CACHE BOOL "Enable BOOST build")
  endif (NOT Boost_FOUND AND NOT DEFINED ENABLE_BOOST)

endif (NOT ENABLE_BOOST)
set(ENABLE_BOOST "${ENABLE_BOOST}" CACHE BOOL "Enable BOOST build")

if (ENABLE_BOOST)

  git_submodule_init(boost CMakeLists.txt)

  set(BOOST_DEPENDS ICU PATCH ZLIB ZSTD)
  TargetVars(BOOST_DEPENDS)

  if (TARGET ZLIB_BLD)
    set(Z_PREFIX_STR "brl_")
  endif (TARGET ZLIB_BLD)


  ExternalProject_Add(BOOST_BLD
    URL "${CMAKE_CURRENT_SOURCE_DIR}/boost"
    BUILD_ALWAYS ${EXT_BUILD_ALWAYS} ${LOG_OPTS}
    PATCH_COMMAND ${PATCH_EXECUTABLE};-E;-p1;${PATCH_OPTIONS};-i;${CMAKE_CURRENT_SOURCE_DIR}/boost_cmake.patch
    CMAKE_ARGS
    ${BUILD_TYPE_SPECIFIER}
    $<$<BOOL:${ZLIB_TARGET}>:-DZ_PREFIX=ON>
    $<$<BOOL:${ZLIB_TARGET}>:-DZ_PREFIX_STR=${Z_PREFIX_STR}>
    -DZLIB_ROOT=$<$<BOOL:${ZLIB_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -DICU_ROOT=$<$<BOOL:${ICU_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -DZSTD_ROOT=$<$<BOOL:${ZSTD_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -DBOOST_IOSTREAMS_ENABLE_BZIP2=OFF
    -DBOOST_IOSTREAMS_ENABLE_LZMA=OFF
    -DBIN_DIR=${BIN_DIR}
    -DLIB_DIR=${LIB_DIR}
    -DBUILD_SHARED_LIBS=ON
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_BUNDLE_INSTALL_PREFIX}
    -DCMAKE_INSTALL_RPATH=${CMAKE_BUNDLE_INSTALL_PREFIX}/${LIB_DIR}
    -DCMAKE_INSTALL_LIBDIR:PATH=${LIB_DIR}
    LOG_CONFIGURE ${EXT_BUILD_QUIET}
    LOG_BUILD ${EXT_BUILD_QUIET}
    LOG_INSTALL ${EXT_BUILD_QUIET}
    LOG_OUTPUT_ON_FAILURE ${EXT_BUILD_QUIET}
    STEP_TARGETS install
    )

  TargetInstallDeps(BOOST BOOST_DEPENDS)

  # Copy the license into position in CMAKE_BUNDLE_INSTALL_PREFIX
  file(MAKE_DIRECTORY ${CMAKE_BUNDLE_INSTALL_PREFIX}/doc/legal/other)
  configure_file(
    ${CMAKE_CURRENT_SOURCE_DIR}/boost/LICENSE_1_0.txt
    ${CMAKE_BUNDLE_INSTALL_PREFIX}/doc/legal/other/boost.txt
    COPYONLY
    )

endif (ENABLE_BOOST)

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

